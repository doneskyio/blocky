apply plugin: "java"

getConfigurations().create("antlr")

sourceSets {
    main {
        java.srcDirs += "$buildDir/antlr/java"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_12
}

dependencies {
    compile "com.squareup:kotlinpoet:1.3.0"
    compile "org.antlr:antlr4-runtime:4.7.2"

    antlr "org.antlr:antlr4:4.7.2"
}

task generateGrammarSource(type: JavaExec) {
    def workingDirectory = file(project.projectDir.path + "/src/main/antlr")
    executable = "java"
    classpath = project.configurations.antlr
    workingDir = workingDirectory
    main = "org.antlr.v4.Tool"
    args = [
        "-o",
        "$buildDir/antlr/java/blocky",
        "-package",
        "blocky",
        "BlockyLexer.g4",
        "BlockyParser.g4"
    ]
    inputs.dir workingDirectory
    outputs.dir file("$buildDir/antlr/java")
}

task updateLexerTokens(type: Copy) {
    from file("$buildDir/antlr/java/blocky")
    into file(project.projectDir.path + "/src/main/antlr")
    include "BlockyLexer.tokens"
}

updateLexerTokens.dependsOn(generateGrammarSource)
compileJava.dependsOn(updateLexerTokens)